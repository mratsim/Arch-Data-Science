# Maintainer: Mamy Ratsimbazafy <mamy (dot) ratsimbazafy_pkgbuild [at] gadz (dot) org>

pkgname=python-hdbscan-git
_pkgname=hdbscan
pkgver=0.8.6.r33.g58fcf1f
pkgrel=1
pkgdesc='Hierarchical Density-Based Spatial Clustering of Applications with Noise'
arch=('any')
url='https://github.com/scikit-learn-contrib/hdbscan'
license=('custom:MIT')
depends=('python-numpy' 'python-scipy' 'python-scikit-learn' 'cython')
makedepends=('git' 'python-setuptools')
conflicts=('python-hdbscan')
provides=('python-hdbscan')
source=('git://github.com/scikit-learn-contrib/hdbscan')
md5sums=('SKIP' )

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root=${pkgdir} --optimize=1

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/python-hdbscan
}
