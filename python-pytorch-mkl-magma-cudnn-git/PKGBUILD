# Update by mratsim: MKL and MAGMA support
# Maintainer: Stephen Zhang <zsrkmyn at gmail dot com>

pkgbase="python-pytorch-git"
pkgname=("python-pytorch-mkl-magma-cudnn-git" "python2-pytorch-mkl-magma-cudnn-git")
_pkgname="pytorch"
pkgver=0.1.11.r2305.gdc6c9e8df
pkgrel=1
pkgdesc="Tensors and Dynamic neural networks in Python with strong GPU acceleration"
arch=('x86_64')
url="https://github.com/pytorch/pytorch"
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools' 'gcc6' 'cmake' 'python-yaml' 'python2-yaml')
depends=('cuda' 'cudnn' 'intel-mkl')
optdepends=('magma: Further CPU, OpenCL, CUDA, optimized operations')
source=("git://github.com/pytorch/pytorch.git")
sha256sums=('SKIP')

pkgver () {
  cd "${_pkgname}"
  (
    set -o pipefail
    git describe --tag 2>/dev/null | sed -e 's/\([^-]*-g\)/r\1/;s/-/./g' -e 's/^v//g'
  )
}


prepare() {
  cd "$srcdir"
  cp -a "${_pkgname}" "${_pkgname}-py2"
  cd "${_pkgname}"
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {

  export MKL_ROOT=/opt/intel/mkl
  export MKL_INCLUDE=$MKL_ROOT/include
  export MKL_LIBRARY=$MKL_ROOT/lib/intel64
  source /opt/intel/mkl/bin/mklvars.sh intel64
  source /opt/intel/bin/compilervars.sh intel64 # For MKL 2018, this is a symlink to /opt/intel/compilers_and_libraries_2018.0.128/linux/bin/compilervars.sh
  export CMAKE_INCLUDE_PATH=$MKL_INCLUDE:$CMAKE_INCLUDE_PATH
  export CMAKE_LIBRARY_PATH=$MKL_LIBRARY:$CMAKE_LIBRARY_PATH


  msg "Building Python 2"
  cd "$srcdir/${_pkgname}-py2"
  git submodule update --init

  CC=gcc-6 \
  CXX=g++-6 \
  WITH_CUDA=1 \
  CUDA_HOME=/opt/cuda \
  WITH_CUDNN=1 \
  CUDNN_LIB_DIR=/opt/cuda/lib64 \
  CUDNN_INCLUDE_DIR=/opt/cuda/include \
  CMAKE_PREFIX_PATH=/opt/magma \
  python2 setup.py build

  msg "Building Python 3"
  cd "$srcdir/${_pkgname}"
  git submodule update --init

  CC=gcc-6 \
  CXX=g++-6 \
  WITH_CUDA=1 \
  CUDA_HOME=/opt/cuda \
  WITH_CUDNN=1 \
  CUDNN_LIB_DIR=/opt/cuda/lib64 \
  CUDNN_INCLUDE_DIR=/opt/cuda/include \
  CMAKE_PREFIX_PATH=/opt/magma \
  python setup.py build
}

package_python2-pytorch-mkl-magma-cudnn-git() {
  conflicts=('python2-pytorch')
  provides=('python2-pytorch')
  depends+=('python2' 'python2-yaml' 'python2-numpy')
  cd "$srcdir/${_pkgname}-py2"
  python2 setup.py install --root="$pkgdir"/ --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python-pytorch-mkl-magma-cudnn-git() {
  conflicts=('python-pytorch')
  provides=('python-pytorch')
  depends+=('python' 'python-yaml' 'python-numpy')
  cd "$srcdir/${_pkgname}"
  python setup.py install --root="$pkgdir"/ --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
